package parser;

import java_cup.runtime.*;   // Library needed for Java-CUP for parsing
import java.util.List;
import java.util.ArrayList;

import source.*;
import tree.Tree;
import tree.DeclNode;
import tree.ConstExp;
import tree.StatementNode;
import tree.ExpNode;
import tree.BinaryOperator;
import tree.UnaryOperator;
import syms.*;
import syms.SymEntry.ProcedureEntry;
import machine.StackMachine;

/** PL0 definition for CUP LALR(1) parser generator.
 * @version $Id: PL0.cup 10 2013-04-15 23:26:08Z uqihayes $
 * This module is responsible for parsing the input file in terms
 * of the tokens supplied by the lexical analyser (see PL0.flex),
 * and generating a symbol table skeleton and an abstract syntax 
 * tree for program.
 * Java-CUP will output a file "CUPParser.java" that contains the parser
 * for PL0. The name "CUPParser.java" is determined by an option to the
 * java_cup command line or to the corresponding ant task.
 * It also outputs CUPToken.java.
 */

parser code 
{:  /* This section provides some methods used by Java_CUP during parsing.
       They override its default methods for reporting syntax errors. */
        
    /** Retrieve the error handler to handle error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** Provides access to the current token position in the input stream */
    public int getCurrentTokenPosition( ) {
        return cur_token.left;
    }
    
    /** Override the default CUP syntax_error method with 
     * one that integrates in with better error reporting. */
    public void syntax_error( Symbol cur_token ) {
        errors.errorMessage( "PL0 syntax error", 
            Severity.ERROR, new Position( getCurrentTokenPosition() ) );
    }
    /** Override the default CUP unrecovered_syntax_error method with 
     * one that integrates in with better error reporting.
     */
    public void unrecovered_syntax_error( Symbol cur_token ) {
        errors.errorMessage( "PL0 unrecovered syntax error",
            Severity.ERROR, new Position( getCurrentTokenPosition() ) );
    }
            
:}

action code 
{:  /* This section provides global variables and methods used in the
       semantics actions associated with parsing rules. */
    /* These are the only global variables you should need. */
    /** Error handler for reporting error messages. */
    private Errors errors = ErrorHandler.getErrorHandler();
    
    /** Symbol table is available globally.
     *  Its current scope corresponds to the procedure/main program 
     *  being processed.
     */
    SymbolTable symtab;
    
    /** Methods for reporting an error message.
     * @param posn gives the position in the program being compiled */
    private void error( String message, Position posn ) {
        errors.errorMessage( message, Severity.ERROR, posn );
    }
    /** @param posn in this case is an integer representing the character
     * position, starting from zero, in the source file. */
    private void error( String message, int posn ) {
        error( message, new Position( posn ) );
    }
    private void fatal( String message ) {
        errors.errorMessage( message, Severity.FATAL, Position.NO_POSITION );
    }

:}


/*--------------------------------------------------------------*
 *--------------------- TERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * CUP generates a file CUPToken.java containing the Java definitions 
 * generated by these. 
 * CUPToken.java is used to communicate with the lexical analyser. 
 * The name "CUPToken.java" is determined by a option to the java_cup  
 * (specified in command line invoking java-cup or in the 
 * corresponding ant task).
 * See PL0.flex for the detailed definitions of the lexical tokens.
 */
terminal
    LPAREN,       /* ( */
    RPAREN,       /* ) */
    SEMICOLON,    /* ; */
    COLON,        /* : */
    ASSIGN,       /* := */
    PLUS,         /* + */
    MINUS,        /* - */
    TIMES,        /* * */
    DIVIDE,       /* / */
    EQUALS,       /* = */
    NEQUALS,      /* != */
    LEQUALS,      /* <= */
    GEQUALS,      /* >= */
    LESS,         /* < */
    GREATER,      /* > */
    RANGE,        /* .. */
    PERIOD,       /* . */
    COMMA,        /* , */
    POINTER,      /* ^ */
    LCURLY,       /* { */
    RCURLY,       /* } */
    KW_BEGIN,     /* begin */
    KW_CALL,      /* call */
    KW_CONST,     /* const */
    KW_DO,        /* do */
    KW_ELSE,      /* else */
    KW_END,       /* end */
    KW_IF,        /* if */
    KW_NEW,       /* new */
    KW_PROCEDURE, /* procedure */
    KW_READ,      /* read */
    KW_RECORD,    /* record */
    KW_THEN,      /* then */
    KW_TYPE,      /* type */
    KW_VAR,       /* var */
    KW_WHILE,     /* while */
    KW_WRITE,     /* write */
    ILLEGAL;      /* illegal symbol - not used in grammar */

/** The following terminal symbols have an attribute associated with them.
 */
terminal String  IDENTIFIER;   /* identifier */
terminal Integer NUMBER;       /* number */


/**-------------------------------------------------------------*
 *------------------ NONTERMINAL SYMBOLS -----------------------*
 *--------------------------------------------------------------*
 * Many nonterminals (but not all) have an attribute type associated 
 * with them. This must be a Java class (subclass of Object, e.g., 
 * Integer rather than int).
 * This gives the type of RESULT in the grammar rules in which the 
 * nonterminal appears on the left side of a grammar rule, and the 
 * type of the variable corresponding to the label attached to 
 * occurrences of the nonterminal on the right side of a rule.
 *
 * The attributes are used to construct the symbol table for the
 * declaration parts and the abstract syntax tree for the bodies of
 * procedures and the main program.
 */
non terminal Tree.ProgramNode Program;
non terminal SymEntry.ProcedureEntry ProgramHead;
non terminal Tree.BlockNode   Block;
non terminal DeclNode.DeclListNode DeclarationList;
non terminal DeclNode         Declaration;
non terminal                  ConstDefSeq, ConstDef;
non terminal ConstExp         Constant;
non terminal                  TypeDefSeq, TypeDef;
non terminal Type             Type, SubrangeType;
non terminal                  VarDeclSeq, VarDecl;
non terminal Type             TypeIdentifier; 
non terminal DeclNode.ProcedureNode ProcedureDef;
non terminal SymEntry.ProcedureEntry ProcedureHead;
non terminal 
                              FormalParamList;
non terminal 
                              ActualParamList;
non terminal StatementNode.ListNode StatementList;
non terminal StatementNode 
                              Statement,
                              CompoundStatement;
non terminal ExpNode          Condition,
                              RelCondition,
                              Exp,
                              Term,
                              Factor;
non terminal BinaryOperator   Relation;
non terminal BinaryOperator   AddOp;
non terminal BinaryOperator   MulOp;
non terminal ExpNode          LValue;

/**-------------------------------------------------------------*
 *------------------------ GRAMMAR RULES -----------------------*
 *--------------------------------------------------------------*
 * Grammar productions have actions associated with them, which
 * are executed after recognising the right side of the production
 * (or in some cases a prefix of the right side). 
 * The action is Java code contained between '{:' and ':}'
 * It is allowed to reference the values of attributes of symbols on 
 * the right side (the name of these attributes is the name after the 
 * ':' following a symbol, e.g., in the rule for Program the attribute 
 * associated with Block is 'block'), and assignments to the attribute of 
 * the left side nonterminal (which is always referred to as 'RESULT').
 * The action code usually comes at the end of the right side of a 
 * production, but it is possible to have multiple actions, each of 
 * which is executed as soon as the symbols preceding it are matched.
 * Be careful to get the syntax of the '{:' and ':}' correct and to
 * make sure they match up, otherwise the error messages get very
 * confusing.
 * To allow reporting of the positions in the input source program
 * of errors, each attribute, such as the id associated with IDENTIFIER
 * in ConstDef, has two further attributes idleft and idright, 
 * representing the positions of the leftmost and rightmost characters 
 * of the construct in the input source program.
 * Note that the name "idleft" is constructed from the name "id" by
 * adding "left" to it. It is a character offset from the start of the
 * source file, but we encapsulate it as a Position as soon as possible.
 *
 * Java-CUP has a special symbol 'error', which is used to provide a 
 * minimal level of recovery from syntax errors in the program. 
 */
Program ::=  ProgramHead:proc Block:block
        {: 
            // This action is executed after the whole program 
            // has been recognised
            block.setProcEntry( proc );
            symtab.leaveScope();
            RESULT = new Tree.ProgramNode( symtab, block );
        :}
    | error
        {:
            /* A null result from Program will cause the compiler to
               avoid further processing. */
            RESULT = null;
        :}
    ;
ProgramHead ::= /* empty */ 
        {:
            // This action occurs before the whole program is recognised.
            /* Construct initial symbol table with current scope the 
             * predefined scope. */
            symtab = new SymbolTable();         
            SymEntry.ProcedureEntry proc = 
                symtab.addProcedure( "<Main>", Position.NO_POSITION );
            if( proc == null ) {
                fatal( "Could not add main program to symbol table" );
            }
            // Enter the scope for the main program
            proc.setLocalScope( symtab.newScope( proc ) );
            RESULT = proc;
        :}
    ;
Block ::= DeclarationList:dl CompoundStatement:s
        {: 
            RESULT = new Tree.BlockNode( dl, s );
        :}
    ;

DeclarationList ::= DeclarationList:dl ProcedureDef:p SEMICOLON
        {:
            dl.addDeclaration(p);
            RESULT = dl;
        :}
    | DeclarationList:dl Declaration 
        {: 
            RESULT = dl;
        :}
    | /* empty */
        {:
            RESULT = new DeclNode.DeclListNode();
        :}
    ;

Declaration ::= KW_CONST ConstDefSeq
    |  KW_TYPE TypeDefSeq
    |  KW_VAR  VarDeclSeq
    ;

ConstDefSeq ::= ConstDef SEMICOLON
    |  ConstDefSeq ConstDef SEMICOLON
    ;

ConstDef ::= IDENTIFIER:id EQUALS Constant:c
        {: 
            /* The attribute idleft represents the position of the start
             * character of the IDENTIFIER token in the input stream.
             * The type Position wraps this to provide a level of abstraction
             * for positions in the input stream.
             */
            Position pos = new Position(idleft);
            /* addConstant returns null if id is already defined 
             * in the current scope */
            if( symtab.addConstant( id, pos, c ) == null ) {
                error( id + " already defined", pos );
            }
        :}
    |  error 
    ;
/* The rules for Constant construct a (mini) abstract syntax tree 
 * for constant expressions (not to be confused with ExpNodes).
 */
Constant ::= NUMBER:n
        {: 
            Position pos = new Position( nleft);
            RESULT = new ConstExp.NumberNode( pos, symtab.getCurrentScope(),
                                              Type.INTEGER_TYPE, n ); 
        :}
    | MINUS:op Constant:c
        {: 
            Position pos = new Position( opleft );
            RESULT = new ConstExp.NegateNode( pos, symtab.getCurrentScope(), 
                                              c ); 
        :}
    |  IDENTIFIER:id
        {: 
            Position pos = new Position( idleft );
            RESULT = new ConstExp.ConstIdNode( pos, symtab.getCurrentScope(),
                                               id );
        :}
    |  error:err
        {:
            RESULT = new ConstExp.ErrorNode( new Position( errleft ),
                                             symtab.getCurrentScope() );
        :}
    ;

TypeDefSeq ::= TypeDef SEMICOLON 
    |  TypeDefSeq TypeDef SEMICOLON
    ;

TypeDef ::= IDENTIFIER:id EQUALS Type:type
        {: 
            Position pos = new Position(idleft);
            if( symtab.addType( id, pos, type ) == null ) {
                error( id + " already defined", pos );
            }
        :}
    |  error 
    ;

Type ::= TypeIdentifier:type
        {: 
            RESULT = type;
        :}
    | SubrangeType:type
        {:
            RESULT = type;
        :}
    | error:err
        {:
            RESULT = Type.ERROR_TYPE;
        :}
    ;
TypeIdentifier ::= IDENTIFIER:id
        {: /* As the type identifier may not be defined at this point
            * the abstract syntax tree records the IDENTIFIER,
            * as well as the symbol table context to look up later.
            */
            RESULT = new Type.IdRefType( id, symtab.getCurrentScope(),
                                         new Position( idleft ) );
        :}
    ;
SubrangeType ::= Constant:lo RANGE Constant:hi
        {:
            RESULT = new Type.SubrangeType( lo, hi );
        :}
    ;

VarDeclSeq ::= VarDecl SEMICOLON
    |  VarDeclSeq VarDecl SEMICOLON
    ;

VarDecl ::= IDENTIFIER:id COLON TypeIdentifier:type
        {: 
            Position pos = new Position(idleft);
            // Variables are always of ReferenceType.
            Type.ReferenceType varType = new Type.ReferenceType( type );
            if( symtab.addVariable( id, pos, varType ) == null ) {
                error( id + " already declared", pos );
            }
        :}
    |  error
    ;
    
ProcedureDef ::= ProcedureHead:proc EQUALS Block:b
        {:
            b.setProcEntry( proc );
            symtab.leaveScope();
            RESULT = new DeclNode.ProcedureNode( proc, b );
        :}
    ;

ProcedureHead ::= KW_PROCEDURE IDENTIFIER:id
            LPAREN FormalParamList:pl RPAREN
        {: 
            SymEntry.ProcedureEntry procEntry;
            /* Add an entry for the procedure to the current scope */
            Position pos = new Position(idleft);
            procEntry = symtab.addProcedure( id, pos );
            if( procEntry == null ) {
                error( id + " already declared", pos );
                /* Create a dummy symbol table entry.
                 * Note that this entry isn't in symtab. */
                procEntry = new SymEntry.ProcedureEntry( id, 
                    pos, symtab.getCurrentScope() );
            }
            /* Create a new scope for the symbols local to
             * the procedure. */
            procEntry.setLocalScope( symtab.newScope( procEntry ) );
            RESULT = procEntry;
        :}
    |  KW_PROCEDURE IDENTIFIER:id error
        {: 
            RESULT = new SymEntry.ProcedureEntry( id, new Position( idleft ),
                            symtab.getCurrentScope() );
            RESULT.setLocalScope( symtab.newScope( RESULT ) );
        :}
    ;

/* To allow for the addition of procedure parameters. */
FormalParamList ::= /* empty */
        {:
        :}
    ;

CompoundStatement ::= KW_BEGIN StatementList:sl KW_END
        {:
            RESULT = sl;
        :}
    ;

StatementList ::= Statement:s
        {:
            RESULT = new StatementNode.ListNode( new Position(sleft) );
            RESULT.addStatement(s);
        :}
    |  StatementList:sl SEMICOLON Statement:s
        {:
            sl.addStatement( s );
            RESULT = sl;
        :}
    ;

Statement ::= KW_WHILE Condition:c KW_DO Statement:s
        {: 
            RESULT = new StatementNode.WhileNode( new Position(cleft), c, s );
        :}
    |  KW_IF Condition:c KW_THEN Statement:s1 KW_ELSE Statement:s2
        {: 
            RESULT = new StatementNode.IfNode( new Position(cleft), c, s1, s2 );
        :}
    |  CompoundStatement:s
        {: 
            RESULT = s; 
        :}
    | KW_READ:r LValue:lval
        {: 
            RESULT = new StatementNode.AssignmentNode( new Position(lvalleft), 
                     lval, new ExpNode.ReadNode( new Position(rleft) ) );
        :}
    |  KW_WRITE Exp:e
        {: 
            RESULT = new StatementNode.WriteNode( new Position(eleft), e );
        :}
    | LValue:lval ASSIGN Condition:rval
       {:
          RESULT = new StatementNode.AssignmentNode( new Position(lvalleft),
                                            lval, rval );
       :} 
    |  KW_CALL IDENTIFIER:id 
            LPAREN ActualParamList:pl RPAREN
        {: 
            RESULT = new StatementNode.CallNode( new Position(idleft), id );
        :}
    |  error:p 
        {: 
            RESULT = new StatementNode.ErrorNode( new Position(pleft) );
        :}
    ;

ActualParamList ::= /* empty */
    ;

LValue ::= IDENTIFIER:id
        {:
            RESULT = new ExpNode.IdentifierNode( new Position(idleft), id );
        :}
    ;

/* To allow for adding logical expressions. */
Condition ::= RelCondition:e
        {: 
            RESULT = e;
        :}
    ;
/* Relatinal operators are lower precedence than arithmetic operators. */
RelCondition ::= Exp:e 
        {:
            RESULT = e;
        :}
    |  Exp:e1 Relation:op Exp:e2
        {:
            RESULT = new ExpNode.BinaryOpNode( new Position( opleft ),
                op, e1, e2 );
        :} 
    | error:e
        {:
            RESULT = new ExpNode.ErrorNode( new Position(eleft) );
        :}
    ;

Relation ::= EQUALS
        {:
            RESULT = BinaryOperator.EQUALS_OP;
        :}
    |  NEQUALS
        {:
            RESULT = BinaryOperator.NEQUALS_OP;
        :}
    |  LEQUALS
        {:
            RESULT = BinaryOperator.LEQUALS_OP;
        :}
    |  LESS
        {:
            RESULT = BinaryOperator.LESS_OP;
        :}
    |  GREATER
        {:
            RESULT = BinaryOperator.GREATER_OP;
        :}
    |  GEQUALS
        {:
            RESULT = BinaryOperator.GEQUALS_OP;
        :}
    ;
/* Expressions are structured into Exp, Term, and Factor to encode the
 * precedence of the operators in the grammar.
 */
Exp ::= Term:t
        {:
            RESULT = t;
        :}
    |  Exp:e1 AddOp:op Term:e2
        {:
            RESULT = new ExpNode.BinaryOpNode(new Position(opleft),op,e1,e2);
        :}
    ;

AddOp ::= PLUS
        {:
            RESULT = BinaryOperator.ADD_OP;
        :}
    |  MINUS
        {:
            RESULT = BinaryOperator.SUB_OP;
        :}
    ;

Term ::= Factor:f
        {:
            RESULT = f;
        :}
    |  Term:e1 MulOp:op Factor:e2
        {:
            RESULT = new ExpNode.BinaryOpNode(new Position(opleft),op,e1,e2);
        :}
    ;

MulOp ::= TIMES
        {:
            RESULT = BinaryOperator.MUL_OP;
        :}
    |  DIVIDE
        {:
            RESULT = BinaryOperator.DIV_OP;
        :}
    ;

Factor ::= PLUS Factor:e
        {:
            RESULT = e; 
        :}
    |  MINUS:op Factor:e
        {:
            Position pos = new Position( opleft );
            RESULT = new ExpNode.UnaryOpNode( pos, UnaryOperator.NEG_OP, e );
        :}
    |  LPAREN Condition:c RPAREN
        {:
            RESULT = c;
        :}
    |  NUMBER:n
        {:
            Position pos = new Position( nleft );
            RESULT = new ExpNode.ConstNode( pos, Type.INTEGER_TYPE, 
                                            n.intValue() );
        :}
    |  LValue:lval
        {:
            RESULT = lval;
        :}
    ;

